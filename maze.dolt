MAZE,
@INIT,
DO{@DISPLAY.MAIN.MENU,#S^S,S'="q" IF{S=1 @START.GAME;
                                     S=2 @GAME.OPTIONS}},
T("")^#,
END;

INIT
0^SHOW.PATH

DISPLAY.MAIN.MENU
T("")^#,0^I^J,
8^S.ROW,24^S.COL,
DO{I+1^I'>26 0^J,DO{J+1^J'>90 @DRAW.EDGE}},
P(S.ROW,S.COL,"  MM  MM       AAAA     ZZZZZZZZZZ  EEEEEEEEEE")^#,
P(S.ROW+1,S.COL,"MM  MM  MM    AA  AA          ZZZ   EEE")^#,
P(S.ROW+2,S.COL,"MM  MM  MM   AA    AA      ZZZZ     EEEEEEE")^#,
P(S.ROW+3,S.COL,"MM  MM  MM  AA AAAA AA   ZZZ        EEE")^#,
P(S.ROW+4,S.COL,"MM  MM  MM  AA      AA  ZZZZZZZZZZ  EEEEEEEEEE")^#,
P(S.ROW+7,S.COL,"(1) START GAME")^#,
P(S.ROW+8,S.COL,"(2) GAME OPTIONS")^#,
P(S.ROW+9,S.COL,"(Q) EXIT GAME")^#

DRAW.EDGE
IF{I=1 P(I,J,"#")^#;
   I=26 P(I,J,"#")^#;
   J=1 P(I,J,"#")^#;
   J=90 P(I,J,"#")^#}

GAME.OPTIONS
DO{@DISPLAY.OPTIONS.MENU,#S^S,S'="m" IF{S=1 @TOGGLE.SHOW.PATH;
                                        S=2}}

DISPLAY.OPTIONS.MENU
T("")^#,""^I^J,
8^S.ROW,25^S.COL,
DO{I+1^I,I'>26 ""^J,DO{J+1^J,J'>90 @DRAW.EDGE}},
IF{SHOW.PATH=0 P(S.ROW,S.COL,"(1) SHOW PATH ( )")^#;
   SHOW.PATH=1 P(S.ROW,S.COL,"(1) SHOW PATH (X)")^#},
P(S.ROW+1,S.COL,"(M) MAIN MENU")^#

TOGGLE.SHOW.PATH
IF{SHOW.PATH=1 0^SHOW.PATH;SHOW.PATH=0 1^SHOW.PATH}

START.GAME
@INIT.MAZE,
@GENERATE.MAZE.TEST,
@DISPLAY.MAZE,
@PLAY,
@CLOSE

INIT.MAZE
O(/,"P"),
""^I^J,
0^MOVES,
0^DISTANCE,
0^DONE,
$RN(23)+2^/M.START|0,
$RN(87)+2^/M.START|1,
""^/M.END|0,
""^/M.END|1,
26^/M.HEIGHT,
90^/M.WIDTH,
0^/M.DISTANCE,
DO{I+1^I,I'>/M.HEIGHT ""^J,DO{J+1^J,J'>/M.WIDTH @INIT.CELL}}

INIT.CELL
IF{I=1 1^/M[I,J]BORDER;
   I=26 1^/M[I,J]BORDER;
   J=1 1^/M[I,J]BORDER;
   J=90 1^/M[I,J]BORDER;
   0^/M[I,J]BORDER},
1^/M[I,J],
1^/M[I,J]WALLS|0,
1^/M[I,J]WALLS|1,
1^/M[I,J]WALLS|2,
1^/M[I,J]WALLS|3,
""^/M[I,J]BACK|0,
""^/M[I,J]BACK|1,
0^/M[I,J]SOLUTION,
0^/M[I,J]PATH

GENERATE.MAZE
/M.START|0^R,
/M.START|1^C,
@BREAK.CELL(R,C),
DO{DONE'=1 @NEXT.CHOICE}

NEXT.CHOICE
0^COUNT,
;Check TOP cell
IF{/M[R-1,C]'=1;/M[R-1,C]BORDER'=0;@HAS.WALLS(R-1,C) @ADD.TO.LIST(R-1,C)},
;Check BOTTOM cell
IF{/M[R+1,C]'=1;/M[R+1,C]BORDER'=0;@HAS.WALLS(R+1,C) @ADD.TO.LIST(R+1,C)},
;Check LEFT cell
IF{/M[R,C-1]'=1;/M[R,C-1]BORDER'=0;@HAS.WALLS(R,C-1) @ADD.TO.LIST(R,C-1)},
;Check RIGHT cell
IF{/M[R,C+1]'=1;/M[R,C+1]BORDER'=0;@HAS.WALLS(R,C+1) @ADD.TO.LIST(R,C+1)},
IF{COUNT'=0 @FORWARD;
   R'=/M.START|0!(C'=/M.START|1) @BACKTRACK;1^DONE}

HAS.WALLS
IF{/M[@1,@2]WALLS|0+/M[@1,@2]WALLS|1+/M[@1,@2]WALLS|2+/M[@1,@2]WALLS|3'=3 0=1;1=1}

ADD.TO.LIST
COUNT+1^COUNT,@1^/N[COUNT]|0,@2^/N[COUNT]|1

FORWARD
$RN(COUNT)+1^CHOICE,
R^TEMP.R,
C^TEMP.C,
/N[CHOICE]|0^R,
/N[CHOICE]|1^C,
TEMP.R^/M[R,C]BACK|0,
TEMP.C^/M[R,C]BACK|1,
DISTANCE+1^DISTANCE,
IF{DISTANCE>/M.DISTANCE DISTANCE^/M.DISTANCE,R^/M.END|0,C^/M.END|1},
@BREAK.CELL(R,C)

BACKTRACK
/M[R,C]BACK|0^TEMP.R,
/M[R,C]BACK|1^TEMP.C,
TEMP.R^R,
TEMP.C^C,
DISTANCE-1^DISTANCE

BREAK.CELL
0^/M[@1,@2],
0^/M[@1-1,@2]WALLS|1,
0^/M[@1+1,@2]WALLS|0,
0^/M[@1,@2-1]WALLS|2,
0^/M[@1,@2+1]WALLS|3

DISPLAY.MAZE
T("")^#,
""^I^J,
DO{I+1^I,I'>/M.HEIGHT ""^J,DO{J+1^J,J'>/M.WIDTH IF{/M[I,J]=1 P(I,J,"#")^#;P(I,J," ")^#}}},
P(/M.END|0,/M.END|1,"E")^#,
P(0,1,"Moves: "_MOVES)^#,
P(0,20,"Min Moves: "_/M.DISTANCE)^#,
P(27,1,"(W) UP :: (S) DOWN :: (A) LEFT :: (D) RIGHT :: (Q) QUIT")^#

PLAY
/M.START|0^P1,
/M.START|1^P2,
@UPDATE.POSITION(P1,P2,P1,P2),
DO{IF{@GAME.NOT.WON #S^KEY,KEY'="q";0=1} IF{KEY="w" @MOVE.UP;
                                            KEY="a" @MOVE.LEFT;
                                            KEY="s" @MOVE.DOWN;
                                            KEY="d" @MOVE.RIGHT}},
IF{P1=/M.END|0&(P2=/M.END|1) P(13,45,"YOU WIN")^#,#0}

GAME.NOT.WON
IF{P1=/M.END|0&(P2=/M.END|1) 0=1;1=1}

MOVE.UP
IF{/M[P1-1,P2]'=1 @UPDATE.MOVES,@UPDATE.POSITION(P1,P2,P1-1,P2)}

MOVE.LEFT
IF{/M[P1,P2-1]'=1 @UPDATE.MOVES,@UPDATE.POSITION(P1,P2,P1,P2-1)}

MOVE.DOWN
IF{/M[P1+1,P2]'=1 @UPDATE.MOVES,@UPDATE.POSITION(P1,P2,P1+1,P2)}

MOVE.RIGHT
IF{/M[P1,P2+1]'=1 @UPDATE.MOVES,@UPDATE.POSITION(P1,P2,P1,P2+1)}

UPDATE.MOVES
MOVES+1^MOVES,
P(0,1,"Moves: "_MOVES)^#

UPDATE.POSITION
IF{SHOW.PATH=0 P(@1,@2," ")^#;SHOW.PATH=1 P(@1,@2,"*")^#},
P(@3,@4,"X")^#,
P(@3,@4)^#,
@3^P1,
@4^P2,
1^/M[P1,P2]PATH

CLOSE
C(/)
;
;Test Macros

SHOW.WALLS
P(0,40,/M[R,C]WALLS|0,/M[R,C]WALLS|1,/M[R,C]WALLS|2,/M[R,C]WALLS|3)^#

GENERATE.MAZE.TEST
/M.START|0^R,
/M.START|1^C,
@BREAK.CELL(R,C),
@DISPLAY.MAZE,
DO{DONE'=1 @NEXT.CHOICE,@SHOW.WALLS,P(TEMP.R,TEMP.C," ")^#,P(R,C,"X")^#,P(R,C)^#,#0}
